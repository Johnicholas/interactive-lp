name ParseFn

terminal PRED 
terminal STAGE
terminal CONTEXT
terminal IDENT of string
terminal HASHDENT of string
terminal LBRACE
terminal RBRACE
terminal LPAREN
terminal RPAREN
terminal PERIOD
terminal COLON    precl 20 /* nonassoc */
terminal COMMA    precl 10
terminal EQUALS
terminal USCORE
terminal DOLLAR   precl 60
terminal BANG     precl 60
terminal STAR     precl 50
terminal LARROW   precl 30
terminal RARROW   precr 40
terminal LLOLLI   precl 30
terminal RLOLLI   precr 40

start Tops

nonterminal Top: top = 
   1:Syn PERIOD                                                 => Decl
   STAGE 1:IDENT OpEquals LBRACE 2:Tops RBRACE OpPeriod         => Stage
   CONTEXT 1:IDENT OpEquals LBRACE 2:Syncommas RBRACE OpPeriod  => Context
   1:HASHDENT 2:Atomics PERIOD                                  => Special

nonterminal Tops: tops =       => NilT
   1:Top 2:Tops                => ConsT

nonterminal Syn: syn = 
   1:Syn COLON 2:Syn           => Ascribe
   1:Syn LLOLLI 2:Syn          => LolliL
   1:Syn LARROW 2:Syn          => ArrowL
   1:Syn RLOLLI 2:Syn          => Lolli
   1:Syn RARROW 2:Syn          => Arrow  
   1:Syn STAR 2:Syn            => Star

   BANG 1:Syn                  => Bang
   DOLLAR 1:Syn                => Dollar

   STAGE 1:IDENT               => StagePred
   1:Atomic 2:Atomics          => App

nonterminal Atomics: syns =    => Nil
   1:Atomic 2:Atomics          => Cons precl 90

nonterminal Atomic: syn =
   LPAREN 1:Syn RPAREN         => parens
   1:IDENT                     => Id
   USCORE                      => Wild
   PRED                        => Pred

nonterminal Syncommas: syns =  => Nil
   1:Syn                       => Single
   1:Syn COMMA 2:Syncommas     => Cons
   
nonterminal OpEquals: ign =    => Ign
   EQUALS                      => Ign

nonterminal OpPeriod: ign =    => Ign
   PERIOD                      => Ign
